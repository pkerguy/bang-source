buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
  }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

sourceSets {
  main {
    resources {
      srcDirs "src/main/resources"
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://devrepo.mckz.org"
  }
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
  jcenter()
}

shadowJar {
  baseName = 'LoaderClient-2.0'
  classifier = null
  version = null
}

dependencies {
  compile project(":app")
  compile "com.samskivert:samskivert:1.9"
  compile "com.threerings:nenya-core:${nenyaVersion}"
  compile "com.threerings:narya:${naryaVersion}"
  compile "com.threerings:vilya:1.6"
  compile "com.threerings:getdown:1.4"
  compile "com.threerings:ooo-util:1.4.1"
  compile "commons-io:commons-io:1.4"
  compile "org.jcraft:jorbis:0.0.17"
  compile "com.badlogicgames.gdx:gdx-backend-lwjgl:${gdxVersion}"
  compile group: "com.badlogicgames.gdx", name: "gdx-platform", version: gdxVersion,
          classifier: "natives-desktop"
  compile "javax.mail:mail:1.4.5"
  compile "javax.activation:activation:1.1.1"
  compile "com.jmr:NitroNet:1.0"
  compile "org.quartz-scheduler:quartz:2.2.1"
  compile "org.quartz-scheduler:quartz-jobs:2.2.1"
  compile 'org.cryptomator:cryptofs:1.5.2'
  
  testCompile "junit:junit:4.8.1"
}

task copyConfig (type: Copy) {
  from "../../etc/${deployment}"
  into sourceSets.main.output.classesDir
  include "deployment.properties"
}
processResources.dependsOn "copyConfig"

task deploy (type: Sync) {
  from jar
  from configurations.runtime
  into project.file("../../build/client")
}


task genService << {
  ant.taskdef(classpath: configurations.tools.asPath,
              resource: "com/threerings/presents/tools.properties")

  ant.path(id: "classpath", location: configurations.compile.asPath)
  ant.genservice(header: "../../lib/SOURCE_HEADER",
                 classpathref: "classpath") {
    fileset(dir: "src/main/java", includes: "**/*Service.java")
  }
}
